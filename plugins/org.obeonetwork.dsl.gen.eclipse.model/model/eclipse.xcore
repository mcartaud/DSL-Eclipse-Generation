@Ecore(nsURI="http://www.obeonetwork.org/dsl/eclipse", nsPrefix="eclipse")
@GenModel(editDirectory="/org.obeonetwork.dsl.gen.eclipse.model.edit/src-gen")
@GenModel(editorDirectory="/org.obeonetwork.dsl.gen.eclipse.model.editor/src-gen")
package org.obeonetwork.dsl.gen.eclipse

import org.eclipse.emf.ecore.EClass
import org.eclipse.emf.ecore.EPackage
import org.eclipse.emf.ecore.EReference

class Application {
	String name
	String applicationID
	String provider
	String copyright
	String version
	String description
	contains Project[] projects
}

abstract class Project {
	String name
	String basePackage
}

class Repository extends Project {
	String categoryLabel
	String categoryID
	String categoryDescription
	refers Feature[] features
	refers Bundle[] bundles
}

class Feature extends Project {
	String copyright
	String description
	String version
	String license
	String label
	String featureProvider
	refers Bundle[] bundles
}

class Bundle extends Project {
	String version
	String requiredEnvironment
	String vendor
	contains ProvidedService[] providedServices
	contains RequiredService[] requiredServices
	contains ImportedPackageDeclaration[] importedPackageDeclarations
	contains Nature[] natures
	contains Builder[] builders
	contains Wizard[] wizards
	contains Extension[] extensions
	contains Decorator[] decorators
	contains Marker[] markers
	contains Perspective[] perspectives
	contains Editor[] editors
	contains View[] views
	refers EPackage[] exportedPackages
	refers EPackage[] ownedPackages
 	refers derived EPackage[] allExportedPackages
}

class ImportedPackageDeclaration {
	String packageName
	String packageVersion
}

class ProvidedService {
	String name
	refers EClass implementation
	refers EClass ^interface
	refers RequiredService[] requiredServices
}

class RequiredService {
	String name
	Integer lowerBound
	Integer upperBound
	refers EClass ^interface
	refers EClass implementation
	refers EReference bindedReference
}

class Builder {
	String name
	refers Nature[] natures opposite builders
}

class Nature {
	String name
	String langageName
	boolean hasToogleNature
	refers Builder[] builders opposite natures
}

class Wizard {
	String name
	String title
	String description
	String category
	String icon
	boolean isProject
}

class ProjectWizard extends Wizard {
	refers Nature[] natures
}

class Extension {
	String name
	boolean required
	contains Attribute[] attributes
}

class Attribute {
	String name
	boolean required
	boolean translatable
	Type ^type
}

class Decorator {
	String label
	String icon
	String name
	boolean isLightweight
	boolean isAdaptable
	Location location
}

class Marker {
	String name
	String markerID
	boolean isPersistant
}

class Perspective {
	String name
	String icon
	contains ActionSet[] actionSets
	refers View[] views opposite perspectives
}

abstract class Part {
	String name
	String icon
	contains ActionSet[] actionSets
	contains DynamicHelp dynamicHelp
}

class PartCategory {
	String name
	contains Part[] parts
}

class View extends Part {
	String category
	boolean isTree
	contains Action[] actions
	contains Menu[] menus
	refers Perspective[] perspectives opposite views
}

class Editor extends Part {
	String extension
}

class ActionSet {
	String name
	contains Menu[] menus
	contains Action[] actions
}

class Menu {
	String name
	contains Menu[] menus
	contains Action[] actions
}

class Action {
	String name
}

class DynamicHelp {
	String name
	String description
	String label
	String title
}

enum Type {
	string
	^boolean
	java
	resource
	identifier
}

enum Location {
	BottomRight
	BottomLeft
	TopRight
	TopLeft
	Underlay
	Blank
}